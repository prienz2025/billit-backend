import org.springframework.boot.gradle.plugin.SpringBootPlugin

plugins {
    id 'java'
    id 'org.springframework.boot' version '3.4.2' apply false
    id 'io.spring.dependency-management' version '1.1.7' apply false
}

subprojects {

    apply plugin: 'java'
    apply plugin: 'io.spring.dependency-management'

    group = 'com.billit'
    version = '0.0.1'

    repositories {
        mavenCentral()
    }

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(17)
        }
    }

    configurations {
        compileOnly {
            extendsFrom annotationProcessor
        }
    }

    dependencyManagement {
        imports {
            mavenBom SpringBootPlugin.BOM_COORDINATES
        }
    }

    dependencies {
        // lombok
        compileOnly 'org.projectlombok:lombok'
        annotationProcessor 'org.projectlombok:lombok'
        testAnnotationProcessor 'org.projectlombok:lombok'
        testImplementation 'org.projectlombok:lombok'

        // test
        testImplementation 'org.springframework.boot:spring-boot-starter-test'
        testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    }

    tasks.named('test') {
        useJUnitPlatform()
        jvmArgs '-Xshare:off'
    }

    ext {
        applyJpa = {
            dependencies {
                implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
            }
        }

        applyQueryDsl = {
            dependencies {
                implementation 'io.github.openfeign.querydsl:querydsl-jpa-spring:6.10.1'
                implementation 'io.github.openfeign.querydsl:querydsl-jpa:6.10.1'
                annotationProcessor 'io.github.openfeign.querydsl:querydsl-apt:6.10.1:jpa'
                annotationProcessor 'jakarta.annotation:jakarta.annotation-api'
                annotationProcessor 'jakarta.persistence:jakarta.persistence-api'
            }
        }

        applyWeb = {
            dependencies {
                implementation 'org.springframework.boot:spring-boot-starter-web'
                implementation 'org.springframework.boot:spring-boot-starter-actuator'
            }
        }
    }
}